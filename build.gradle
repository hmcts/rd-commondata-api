buildscript {
  dependencies {
    classpath("net.serenity-bdd:serenity-gradle-plugin:2.3.6")
  }
}
plugins {
  id 'application'
  id 'uk.gov.hmcts.java' version '0.12.12'
  id 'pmd'
  id 'jacoco'
  id "org.flywaydb.flyway" version "7.0.2"
  id 'io.spring.dependency-management' version '1.1.0'
  id 'org.springframework.boot' version '2.7.7'
  id 'com.github.ben-manes.versions' version '0.39.0'
  id 'org.sonarqube' version '3.3'
  id "info.solidsoft.pitest" version '1.7.0'
  id 'au.com.dius.pact' version '4.1.7' //do not change, otherwise serenity report fails
}

apply plugin: 'java'
apply plugin: 'net.serenity-bdd.aggregator'
apply plugin: 'project-report'
apply plugin: 'idea'

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

def versions = [
  lombok          : '1.18.22',
  gradlePitest    : '1.5.1',
  pitest          : '1.7.3',
  sonarPitest     : '0.5',
  reformLogging   : '5.1.7',
  pact_version    : '4.1.7',
  log4j           : '2.17.1',
  launchDarklySdk : "5.8.1",
  reformS2sClient : '4.0.0',
  serenity        : '2.0.76',
  restAssured     : '4.3.3',
  springVersion   : '5.3.20',
  logback         : '1.2.10',
  feign           : '3.8.0',
  bytebuddy       : '1.12.7',
  testContainer_postgresql: '1.17.6'

]

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }

  contractTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/contractTest/java')
    }
    resources.srcDir file('src/contractTest/resources')
  }
  pactTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/pactTest/java')
    }
    resources.srcDir file('src/pactTest/resources')
  }
}

tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  failFast = true
}

task functional(type: Test) {
  description = "Runs functional tests"
  group = "Verification"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
  finalizedBy aggregate
}

task integration(type: Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
}

task migratePostgresDatabase(type: org.flywaydb.gradle.task.FlywayMigrateTask) {
  baselineOnMigrate = true
  if (project.hasProperty("dburl")) {
    url = "jdbc:postgresql://${dburl}"
  }
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
  finalizedBy aggregate
}

project.ext {
  pactVersion = getCheckedOutGitCommitHash()
}

task contract(type: Test) {
  description = 'Runs the consumer Pact tests'
  useJUnitPlatform()
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
  include "uk/gov/hmcts/reform/cdapi/**"

}

task runProviderPactVerification(type: Test) {
  useJUnitPlatform()
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
  systemProperty 'pact.verifier.publishResults', System.getProperty('pact.verifier.publishResults')
  systemProperty 'pact.provider.version', project.pactVersion
}

task fortifyScan(type: JavaExec)  {
  main = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
  ignoreExitValue = true
}

runProviderPactVerification.dependsOn contract

runProviderPactVerification.finalizedBy pactVerify

def getCheckedOutGitCommitHash() {
  'git rev-parse --verify --short HEAD'.execute().text.trim()
}

pact {
  publish {
    pactDirectory = 'pacts'
    pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
    tags = [System.getenv("PACT_BRANCH_NAME") ?:'Dev']
    version = project.pactVersion
  }
}

pmd {
  toolVersion = "6.12.0"
  ignoreFailures = true
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  // https://github.com/pmd/pmd/issues/876
  ruleSets = [
    'category/java/errorprone.xml',
    'category/java/bestpractices.xml'
  ]
}

jacoco {
  toolVersion = "0.8.7"
}
jacocoTestCoverageVerification {
  violationRules {
    rule {
      limit {
        counter = 'LINE'
        value = 'COVEREDRATIO'
        minimum = 0.00
      }
    }
  }
}

jacocoTestReport {
  executionData(test)
  reports {
    xml.required = true
    csv.required = false
    xml.outputLocation = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

pitest {
  junit5PluginVersion = '0.15'
  targetClasses = ['uk.gov.hmcts.reform.cdapi.*']
  excludedClasses = [
    'uk.gov.hmcts.reform.cdapi.config.*',
    'uk.gov.hmcts.reform.cdapi.domain.*'
  ]
  threads = 10
  enableDefaultIncrementalAnalysis = true
  historyInputLocation = 'build/reports/pitest/fastermutationtesting'
  historyOutputLocation = 'build/reports/pitest/fastermutationtestingoutput'
  outputFormats = ['XML', 'HTML']
  timestampedReports = false
  mutationThreshold = 85
  useClasspathFile = true
}

project.tasks['sonarqube'].dependsOn test, integration, jacocoTestReport

sonarqube {
  properties {
    property "sonar.projectName", "RD :: Common Data API"
    property "sonar.projectKey", "RDCOMMONDATAAPI"
    property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    property "sonar.pitest.mode", "reuseReport"
    property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    property "sonar.exclusions", "src/main/java/uk/gov/hmcts/reform/cdapi/config/**," +
      "src/main/java/uk/gov/hmcts/reform/cdapi/CommonDataApplication.java," +
      "src/main/java/uk/gov/hmcts/reform/cdapi/domain/**"
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

dependencyManagement {

  dependencies {
    // CVE-2021-42340
    dependencySet(group: 'org.apache.tomcat.embed', version: '9.0.69') {
      entry 'tomcat-embed-core'
      entry 'tomcat-embed-el'
      entry 'tomcat-embed-websocket'
    }
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  suppressionFile = 'config/owasp/suppressions.xml'

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
  maven { url 'https://jitpack.io' }
}

dependencies {
  testImplementation ('com.github.hmcts:rd-commons-lib:v0.0.13'){
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-web'
  }


  implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-resource-server'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-client'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-cache'
  implementation group: 'com.fasterxml.jackson', name:'jackson-bom' , version:'2.13.2.20220324'
  implementation group: 'org.postgresql', name: 'postgresql', version: '42.5.1'
  implementation group: 'org.flywaydb', name: 'flyway-core', version: '8.5.12'
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
  implementation group: 'org.yaml', name: 'snakeyaml', version: '1.33'

  implementation group: 'com.launchdarkly', name: 'launchdarkly-java-server-sdk', version: '5.10.2'
  implementation group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: versions.reformS2sClient
  implementation group: 'javax.inject', name: 'javax.inject', version: '1'
  implementation "com.github.hmcts.java-logging:logging:${versions.reformLogging}"
  implementation "com.github.hmcts.java-logging:logging-appinsights:${versions.reformLogging}"
  implementation group: 'uk.gov.hmcts.reform', name: 'idam-client', version: '2.0.0'
  implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '2.8.5'
  implementation group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.1'
  implementation(group: 'io.rest-assured', name: 'rest-assured', version: '3.3.0') {
    exclude group: "com.sun.xml.bind", module: "jaxb-osgi"
    exclude group: "org.apache.sling"
  }
  implementation 'commons-io:commons-io:2.8.0'
  implementation group: 'com.sun.xml.bind', name: 'jaxb-osgi', version: '2.3.3'
  implementation group: 'ch.qos.logback', name: 'logback-core', version: versions.logback
  implementation group: 'ch.qos.logback', name: 'logback-classic', version: versions.logback
  implementation group: 'org.springframework', name: 'spring-core', version: versions.springVersion
  implementation group: 'org.springframework.security', name: 'spring-security-core', version: '5.7.5'
  implementation group: 'org.springframework', name: 'spring-beans', version: versions.springVersion
  implementation group: 'org.springframework', name: 'spring-aop', version: versions.springVersion
  implementation group: 'org.springframework', name: 'spring-aspects', version: versions.springVersion
  implementation group: 'org.springframework', name: 'spring-context-support', version: versions.springVersion
  implementation group: 'org.springframework', name: 'spring-context', version: versions.springVersion
  implementation group: 'org.springframework.security', name: 'spring-security-test'

  implementation group: 'org.springframework', name: 'spring-expression', version: versions.springVersion
  implementation group: 'org.springframework', name: 'spring-jcl', version: versions.springVersion
  implementation group: 'org.springframework', name: 'spring-jdbc', version: versions.springVersion
  implementation group: 'org.springframework', name: 'spring-orm', version: versions.springVersion
  implementation group: 'org.springframework', name: 'spring-tx', version: versions.springVersion
  implementation group: 'org.springframework', name: 'spring-web', version: versions.springVersion
  implementation group: 'org.springframework', name: 'spring-webmvc', version: versions.springVersion
  implementation group: 'com.fasterxml.jackson', name: 'jackson-bom', version: '2.13.2.20220324', ext: 'pom'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: versions.log4j
  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: versions.log4j
  implementation group: 'io.github.openfeign.form', name: 'feign-form', version: versions.feign
  implementation group: 'io.github.openfeign.form', name: 'feign-form-spring', version: versions.feign
  implementation "io.github.openfeign:feign-httpclient:11.0"
  implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: versions.log4j

  implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.6.8'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: '3.1.5'

//  testImplementation group: 'org.testcontainers', name: 'postgresql', version: '1.17.2'
//  testImplementation "org.testcontainers:junit-jupiter:1.17.2"
//  testImplementation "org.testcontainers:testcontainers:1.16.2"

  testImplementation group: 'org.testcontainers', name: 'postgresql', version: versions.testContainer_postgresql
  testImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: versions.testContainer_postgresql
  testImplementation group: 'org.testcontainers', name: 'testcontainers', version: versions.testContainer_postgresql

  //implementation group: 'org.postgresql', name: 'postgresql', version: '42.2.14'
  testImplementation(platform('org.junit:junit-bom:5.8.2'))
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }
  testImplementation("com.github.tomakehurst:wiremock-jre8:2.33.2") {
    exclude group: 'com.github.jknack'
  }
  testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '3.3.0'
  testImplementation group: 'io.rest-assured', name: 'rest-assured-common', version: '4.2.0'

  testImplementation ("org.mockito:mockito-core:4.3.1") {
    exclude group: "net.bytebuddy", module: "byte-buddy"
    exclude group: "net.bytebuddy", module: "byte-buddy-agent"
  }
  testImplementation ("org.mockito:mockito-inline:4.3.1") {
    exclude group: "net.bytebuddy", module: "byte-buddy"
    exclude group: "net.bytebuddy", module: "byte-buddy-agent"
  }
  testImplementation group: 'net.bytebuddy', name: 'byte-buddy', version: versions.bytebuddy
  testImplementation group: 'net.bytebuddy', name: 'byte-buddy-agent', version: versions.bytebuddy
  testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.23.1'


  testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '4.3.1'

  testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.7'
  testImplementation group: 'com.github.mifmif', name: 'generex', version: '1.0.2'
  testImplementation 'io.github.openfeign:feign-jackson:11.0'
  testImplementation group: 'org.pitest', name: 'pitest', version: versions.pitest
  testImplementation 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.7.0'
  testImplementation group: 'org.pitest', name: 'pitest-junit5-plugin', version: '0.15'
  testImplementation 'org.codehaus.sonar-plugins:sonar-pitest-plugin:0.5', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }

  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.9.1'



  functionalTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
  functionalTestRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
  functionalTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
  functionalTestImplementation group: 'org.junit.platform', name: 'junit-platform-commons', version: versions.junitPlatform
  functionalTestRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-engine', version: versions.junitPlatform

  smokeTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
  smokeTestRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
  smokeTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
  smokeTestImplementation group: 'org.junit.platform', name: 'junit-platform-commons', version: versions.junitPlatform
  smokeTestRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-engine', version: versions.junitPlatform

  integrationTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
  integrationTestRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
  integrationTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
  integrationTestImplementation group: 'org.junit.platform', name: 'junit-platform-commons', version: versions.junitPlatform
  integrationTestRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-engine', version: versions.junitPlatform

  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact_version
  contractTestRuntimeOnly group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact_version
  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'java8', version: versions.pact_version
  contractTestRuntimeOnly group: 'au.com.dius.pact.consumer', name: 'java8', version: versions.pact_version

  contractTestImplementation("org.junit.jupiter:junit-jupiter-api:5.7.1")
  contractTestRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.8.2")
  contractTestImplementation('org.junit.jupiter:junit-jupiter-api:5.7.1')
  contractTestImplementation group: 'au.com.dius.pact.provider', name: 'junit5', version: versions.pact_version
  contractTestImplementation group: 'au.com.dius.pact.provider', name: 'spring', version: versions.pact_version
  contractTestImplementation group: 'au.com.dius.pact.provider', name: 'junit5spring', version: versions.pact_version

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath

  functionalTestImplementation sourceSets.main.runtimeClasspath
  functionalTestImplementation sourceSets.test.runtimeClasspath

  smokeTestImplementation sourceSets.main.runtimeClasspath
  smokeTestImplementation sourceSets.test.runtimeClasspath

  contractTestImplementation sourceSets.main.runtimeClasspath
  contractTestImplementation sourceSets.test.runtimeClasspath

  pactTestImplementation sourceSets.main.runtimeClasspath
  pactTestImplementation sourceSets.test.runtimeClasspath

  compileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  testCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  integrationTestCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  integrationTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  functionalTestCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  functionalTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  smokeTestCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  smokeTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
}

mainClassName = 'uk.gov.hmcts.reform.cdapi.CommonDataApplication'

bootJar {
  archiveFileName = "rd-commondata-api.jar"

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

// Gradle 7.x issue, workaround from: https://github.com/gradle/gradle/issues/17236#issuecomment-894768083
rootProject.tasks.named("processSmokeTestResources") {
  duplicatesStrategy = 'include'
}

rootProject.tasks.named("processFunctionalTestResources") {
  duplicatesStrategy = 'include'
}

rootProject.tasks.named("processIntegrationTestResources") {
  duplicatesStrategy = 'include'
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}

dependencyManagement {
  dependencies {
    dependencySet(group: 'com.fasterxml.jackson.core', version: '2.14.0-rc1') {
      entry 'jackson-databind'
      entry 'jackson-core'
      entry 'jackson-annotations'
    }
  }
}

